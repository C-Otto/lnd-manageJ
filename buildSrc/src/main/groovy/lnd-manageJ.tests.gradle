plugins {
    id 'java'
    id 'com.adarshr.test-logger'
}

testing {
    suites {
        test {
            useJUnitJupiter()
            dependencies {
                implementation 'nl.jqno.equalsverifier:equalsverifier'
                implementation 'uk.org.lidalia:slf4j-test'
                configurations.named('testRuntimeOnly') {
                    exclude group: 'ch.qos.logback', module: 'logback-classic'
                    exclude group: 'org.slf4j', module: 'slf4j-nop'
                }
            }
        }
        withType(JvmTestSuite) {
            dependencies {
                implementation 'org.springframework.boot:spring-boot-starter-test'
            }
        }
    }
}

tasks.withType(Test).configureEach {
    afterTest { descriptor, result ->
        if (result.resultType == TestResult.ResultType.SKIPPED) {
            throw new GradleException('Do not ignore test cases')
        }
    }
}

testlogger {
    theme 'standard-parallel'
    slowThreshold 1000
    showSimpleNames true
    showOnlySlow true
}
