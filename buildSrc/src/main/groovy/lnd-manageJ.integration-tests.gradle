plugins {
    id 'lnd-manageJ.tests'
}

testing {
    suites {
        integrationTest(JvmTestSuite) {
            testType = TestSuiteType.INTEGRATION_TEST
            dependencies {
                implementation project
            }

            targets {
                all {
                    testTask.configure {
                        shouldRunAfter(test)
                    }
                }
            }
        }
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

tasks.named('check') {
    dependsOn(testing.suites.integrationTest)
}

dependencies {
    testImplementation 'com.tngtech.archunit:archunit:0.21.0'
    testImplementation 'org.awaitility:awaitility:4.1.0'
    configurations.getByName('integrationTestRuntimeOnly').exclude group: 'ch.qos.logback', module: 'logback-classic'
    configurations.getByName('integrationTestRuntimeOnly').exclude group: 'org.slf4j', module: 'slf4j-nop'
}

integrationTest {
    testlogger {
        slowThreshold 2000
    }
}