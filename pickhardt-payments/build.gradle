plugins {
    id 'lnd-manageJ.java-library-conventions'
}

dependencies {
    implementation project(':backend')
    implementation project(':caching')
    implementation project(':configuration')
    implementation project(':model')
    implementation project(':grpc-adapter')
    implementation 'com.google.ortools:ortools-java:9.3.10497'
    implementation 'org.eclipse.collections:eclipse-collections:11.0.0'
    testImplementation testFixtures(project(':model'))
    testImplementation 'org.awaitility:awaitility:4.1.1'
    integrationTestImplementation project(':backend')
    integrationTestImplementation project(':grpc-adapter')
    integrationTestImplementation testFixtures(project(':model'))
    testFixturesImplementation testFixtures(project(':model'))
}

task deleteCrashLogs(type: Delete) {
    delete fileTree(dir:project.projectDir, include: 'hs_err_pid*.log')
}
tasks.findByName('pitest').finalizedBy(deleteCrashLogs)

pitest {
    testStrengthThreshold = 98
}

jacocoTestCoverageVerification {
    violationRules {
        rules.forEach {rule ->
            rule.limits.forEach {limit ->
                if (limit.counter == 'BRANCH') {
                    limit.minimum = 0.98
                }
            }
        }
    }
}
